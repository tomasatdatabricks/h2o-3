@Library('test-shared-library') _

def DEFAULT_NODE_LABEL = 'docker && !mr-0xc8'

def defineTestStages = null
def buildConfig = null
def pipelineEmailContent = null
def emailContentHelpers = null
def sendEmail = null
def result = 'FAILURE'
def scmEnv = null

try {
  ansiColor('xterm') {
    timestamps {

      node(DEFAULT_NODE_LABEL) {
        stage('Prepare and Build') {
          dir('h2o-3') {
            // clear the folder
            deleteDir()
            // checkout H2O-3
            retry(3) {
              timeout(time: 1, unit: 'MINUTES') {
                echo "###### Checkout H2O-3 ######"
                scmEnv = checkout scm
              }
            }
          }

          pipelineEmailContent = load('h2o-3/scripts/jenkins/groovy/pipelineEmailContent.groovy')
          sendEmail = load('h2o-3/scripts/jenkins/groovy/sendEmail.groovy')
          emailContentHelpers = load('h2o-3/scripts/jenkins/groovy/emailContentHelpers.groovy')

          def setBuildDesc = load('h2o-3/scripts/jenkins/groovy/setBuildDescription.groovy')
          setBuildDesc()

          def prepareH2O3 = load('h2o-3/scripts/jenkins/groovy/prepareH2O3.groovy')
          buildConfig = prepareH2O3(scmEnv, 'MODE_NIGHTLY', true)
          buildConfig.setDefaultOverrideRerun(true)

          def setJobProperties = load('h2o-3/scripts/jenkins/groovy/setJobProperties.groovy')
          // Execute nightly for master at 22:XX, 0:XX, 2:XX, 4:XX and 6:XX
          // for rel- branches at 21:XX
          def scheduleString = 'H 22,0,2,4,6 * * *'
          if (env.BRANCH_NAME.startsWith('rel-')) {
            scheduleString = 'H 21 * * *'
          }
          setJobProperties(buildConfig, pipelineTriggers([cron(scheduleString)]))

          // Load the defineTestStages script
          defineTestStages = load('h2o-3/scripts/jenkins/groovy/defineTestStages.groovy')
        }
      }
      defineTestStages(buildConfig)
      result = 'SUCCESS'
    }
  }
} finally {
  if (pipelineEmailContent != null && sendEmail != null && emailContentHelpers != null) {
    def emailBody = pipelineEmailContent(result, emailContentHelpers)
    sendEmail(result, emailBody, emailContentHelpers.getRelevantPipelineRecipients(result))
  }
}